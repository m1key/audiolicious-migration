apply plugin: 'scala'
apply plugin: 'maven'

sourceCompatibility = 1.7
version = '0.0.2-SNAPSHOT'
group = 'me.m1key.audiolicious-migration'

jar {
	manifest {
		attributes 'Implementation-Title': 'Audiolicious Migration', 'Implementation-Version': version,
				'Main-class': 'me.m1key.audioliciousmigration.Launcher'
	}
	from {
		configurations.compile.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
}

test { exclude "**/*IT.class" }

task integrationTest(type: Test, dependsOn: testClasses) { include "**/*IT.class" }

check.dependsOn integrationTest

repositories {
	mavenLocal()
	mavenCentral()
	mavenRepo name: "EclipseLink", url: "http://download.eclipse.org/rt/eclipselink/maven.repo/"
	mavenRepo name: "MongoDB", url: "http://morphia.googlecode.com/svn/mavenrepo"
	mavenRepo name: "Audiolicious", url: "https://github.com/m1key/repo/raw/master/releases"
}

dependencies {
	compile group: 'com.google.inject', name: 'guice', version: '3.0'
	compile group: 'me.m1key.audiolicious', name: 'audiolicious-domain-objects', version: '0.2.1'
	compile group: 'org.scalaj', name: 'scalaj-collection_2.9.1', version: '1.2'

	compile group: 'org.eclipse.persistence', name: 'eclipselink', version: '2.3.0'
	compile group: 'org.eclipse.persistence', name: 'javax.persistence', version: '2.0.0'
	compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.21'
	compile group: 'org.mongodb', name: 'mongo-java-driver', version: '2.7.3'
	compile group: 'com.google.code.morphia', name: 'morphia', version: '0.99'

	testCompile group: 'org.scala-tools.testing', name: 'specs_2.9.1', version: '1.6.9'
	testCompile group: 'junit', name: 'junit', version: '4.+'

	// Libraries needed to run the scala tools
	scalaTools 'org.scala-lang:scala-compiler:2.9.1'
	scalaTools 'org.scala-lang:scala-library:2.9.1'

	//	 Libraries needed for scala api
	compile 'org.scala-lang:scala-library:2.9.1'
}